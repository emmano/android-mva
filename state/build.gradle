apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'digital.wup.android-maven-publish'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.3'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
}

def VERSION = '0.1.0'

publishing {
    publications {
        android.libraryVariants.all { variant ->
            "maven${variant.name.capitalize()}Aar"(MavenPublication) {
                from components.findByName("android${variant.name.capitalize()}")
                groupId 'me.emmano'
                artifactId 'state'
                version VERSION
                artifact sourcesJar
            }
        }
    }
}

bintray {
    user = 'emmano'
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenReleaseAar']

    dryRun = false
    publish = true
    pkg {
        repo = 'emmano'
        name = 'me.emmano.state'
        desc = 'Redux like implementation for the view layer'
        websiteUrl = 'https://github.com/emmano/android-mva/tree/master/state'
        issueTrackerUrl = 'https://github.com/emmano/android-mva/issues'
        vcsUrl = 'https://github.com/emmano/android-mva.git'
        licenses = ['Apache-2.0']
        labels = ['Android', 'State', 'ViewModel']
        publicDownloadNumbers = true

        version {
            name = VERSION //Bintray logical VERSION name
            desc = 'Redux like implementation for the view layer'
            vcsTag = VERSION
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

artifacts {
    archives sourcesJar
}