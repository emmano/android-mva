def VERSION = '0.2.0'

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'digital.wup.android-maven-publish'


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName VERSION

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    testOptions.unitTests.includeAndroidResources = true

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "adapt-${VERSION}.aar"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.recyclerview:recyclerview:1.1.0"

    testImplementation 'junit:junit:4.13'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test:core:1.2.0'
    debugImplementation 'androidx.fragment:fragment-testing:1.2.4'
    testImplementation 'me.emmano:patch:0.2.0'
}

publishing {
    publications {
        android.libraryVariants.all { variant ->
            "maven${variant.name.capitalize()}Aar"(MavenPublication) {
                from components.findByName("android${variant.name.capitalize()}")
                groupId 'me.emmano'
                artifactId 'adapt'
                version VERSION
                artifact sourcesJar
            }
        }
    }
}

bintray {
    user = 'emmano'
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenReleaseAar']

    dryRun = false
    publish = false
    pkg {
        repo = 'emmano'
        name = 'me.emmano.adapt'
        desc = 'DataBinding RecyclerView Adapters made easy'
        websiteUrl = 'https://github.com/emmano/android-mva/tree/master/adapt'
        issueTrackerUrl = 'https://github.com/emmano/android-mva/issues'
        vcsUrl = 'https://github.com/emmano/android-mva.git'
        licenses = ['Apache-2.0']
        labels = ['Android', 'DataBinding', 'RecyclerView']
        publicDownloadNumbers = true

        version {
            name = VERSION //Bintray logical VERSION name
            desc = 'DataBinding RecyclerView Adapters made easy'
            vcsTag = VERSION
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

artifacts {
    archives sourcesJar
}